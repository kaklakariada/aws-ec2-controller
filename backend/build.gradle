plugins {
    id 'java'
    id 'jacoco'
    id "com.diffplug.eclipse.apt" version "3.40.0"
    id "com.github.ben-manes.versions" version "0.45.0"
    id "org.sonarqube" version "4.0.0.2929"
}

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11
compileJava.options.compilerArgs += '-parameters'
compileTestJava.options.compilerArgs += '-parameters'

version = '0.0.1'

ext {
    micronautVersion = '3.8.5'
    micronautAwsVersion = '3.13.1'
    awsSdkVersion = '1.12.410'
}

dependencies {
    annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    annotationProcessor "io.micronaut:micronaut-inject-java"
   	annotationProcessor "io.micronaut:micronaut-validation"

    implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut.crac:micronaut-crac"

    implementation "io.micronaut.aws:micronaut-function-aws-api-proxy:$micronautAwsVersion"
    implementation "com.amazonaws:aws-java-sdk-ec2:$awsSdkVersion"
    implementation "com.amazonaws:aws-java-sdk-dynamodb:$awsSdkVersion"
    implementation "com.amazonaws:aws-java-sdk-route53:$awsSdkVersion"
    implementation "com.amazonaws:aws-java-sdk-pricing:$awsSdkVersion"
    
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation 'io.symphonia:lambda-logging:1.0.3:no-config'
    
    testAnnotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    
    testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.2"
    testImplementation "io.micronaut.test:micronaut-test-junit5:3.1.1"
    testImplementation "org.mockito:mockito-core:5.1.1"
    testRuntimeOnly "com.fasterxml.jackson.module:jackson-module-afterburner:2.14.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
}

test {
    useJUnitPlatform()
}

task runServer(type: JavaExec) {
  doFirst {
    def configFile = file("local-server-env.properties")
    assert configFile.exists() : "Please create config file ${configFile}"
    def environmentProperties = new Properties()
    configFile.withInputStream { environmentProperties.load(it) }
    environment environmentProperties
  }

  classpath sourceSets.main.runtimeClasspath
  jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote', '-Dmicronaut.environments=local')
  mainClass = "org.itsallcode.aws.ec2.Application"
}

task buildLambdaZip(type: Zip) {
    group = 'build'
    archiveFileName = 'backend.zip'
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

build.dependsOn buildLambdaZip

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  gradleReleaseChannel = "current"
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

rootProject.tasks["sonarqube"].dependsOn(tasks["jacocoTestReport"])

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "kaklakariada-github"
    }
}
